/*
 * Copyright 2014 Cyrus Innovation, LLC. Licensed under Apache license 2.0.
 */

repositories {
    maven {
        name = 'filament-site'
        url = 'https://filament.svn.sourceforge.net/svnroot/filament/trunk/m2-repo/'
    }
    maven {
        name = "mjb-repo"
        url = "https://meta-jb.svn.sourceforge.net/svnroot/meta-jb/trunk/dev/m2-repo/"
    }
}

dependencies {
    compile "org.scala-lang:scala-compiler:$scalaVersion"
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile("org.scalatra:scalatra_2.10:2.2.2") {
        exclude module: 'joda-time'
    }
    compile("org.eclipse.jetty:jetty-webapp:9.1.5.v20140505")

    compile "com.googlecode.scalascriptengine:scalascriptengine:1.3.7-$scalaVersion"

    compile project(':core')
    compile project(':persistence')

    testCompile "junit:junit:4.11"
    testCompile "org.scalatest:scalatest_2.10:2.0"
    testCompile("org.scalamock:scalamock-scalatest-support_2.10:3.1.RC1")

    testCompile project(':core').sourceSets.test.output
}

// scaladoc.excludes = ['**com/cyrusinnovation/computation/EvalCode.scala', '**com/cyrusinnovation/computation/util/Logger.scala']
scaladoc.title = 'Computation Engine API Example, version ' + version

def pomConfig = {
    packaging 'jar'
    name 'Computation Engine Store Example Module'
    url 'https://github.com/cyrusinnovation/computation-engine'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/cyrusinnovation/computation-engine.git'
        connection 'scm:git:git://github.com/cyrusinnovation/computation-engine.git'
        developerConnection 'scm:git:git@github.com:cyrusinnovation/computation-engine.git'
    }
    developers {
        developer {
            id 'theunforgiven'
            name 'Nicholas Goodwin'
            roles {
                role 'Contributor'
            }
        }
   }
}

publishing {
    publications {
        code(MavenPublication) {
            artifactId = project.publishedArtifactId

            pom.withXml {
                //Somehow the description node can't go in the pomConfig.
                asNode().appendNode('description', 'An example project to demonstrate how to use the scala computation engine library') + pomConfig
            }
        }
        jarSignatures(MavenPublication) {
           artifactId = project.publishedArtifactId
        }
        pomSignature(MavenPublication) {
            artifactId = project.publishedArtifactId
        }
    }
}